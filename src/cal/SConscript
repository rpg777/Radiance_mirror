import os

Import('env') # inherit from parent

mlib = env['RAD_MLIB']

# standard targets
PROGS = (
# name     sources             libs
('calc',   Split('calc.c'),    ['rtfunc','rtmem','rtcont'] + mlib),
('ev',     Split('ev.c'),      ['rtfunc','rtmem','rtcont'] + mlib),
('rcalc',  Split('rcalc.c'),   ['rtfunc','rtmem','rtcont'] + mlib),
('total',  Split('total.c'),   mlib),
('cnt',    Split('cnt.c'),     []),
('neat',   Split('neat.c'),    []),
('lam',    Split('lam.c'),     ['rtproc','rtpath','rtio','rterror']),
('tabfunc',Split('tabfunc.c'), ['rtio'] + mlib),
('histo',  Split('histo.c'),   mlib),
)
for p in PROGS:
    prog = env.Program(target=os.path.join(env['RAD_BUILDBIN'], p[0]),
			source=p[1], LIBS=p[2])
    Default(prog)
    env.Append(RAD_BININSTALL=[env.Install(env['RAD_BINDIR'], prog)])


