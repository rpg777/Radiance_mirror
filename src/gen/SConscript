import os

Import('env') # inherit from parent

# math libs
mlib = env['RAD_MLIB']

# standard targets
PROGS = (
# name        sources                  libs
('genbeads',  Split('genbeads.c hermite3.c'),  []),
('genbox',    ['genbox.c',],       []),
('genmarble', ['genmarble.c',],    []),
('gensky',    Split('gensky.c sun.c',), []),
('genblinds', ['genblinds.c',],    []),
('genprism',  ['genprism.c',],     []),
('genrev',    ['genrev.c',],       ['rterror','rtfunc','rtcont','rtmem']),
('gencat',    ['gencat.c',],       []),
('genworm',   ['genworm.c',],      ['rtfunc','rtmem','rtcont','rtmath']),
('gensurf',   ['gensurf.c',],      ['rtfunc','rtmem','rtcont','rtmath','rtio']),
('genclock',  ['genclock.c',],     []),
('genbranch', ['genbranch.c',],    []),
('replmarks', ['replmarks.c',],
	['rtproc','rtpath','rtmath','rtio','rterror']),
('mkillum',   Split('mkillum.c mkillum2.c mkillum3.c'),
    ['rtscene','rtproc','rtpath','rtmath','rtio','rtcont','rterror']),
('xform',     ['xform.c',],
    ['rtproc','rtscene','rtmath','rtargs','rtio','rtcont','rtpath','rterror']),
)
for p in PROGS:
    prog = env.Program(target=os.path.join(env['RAD_BUILDBIN'], p[0]),
            source=p[1], LIBS=p[2] + mlib)
    Default(prog)
    env.Append(RAD_BININSTALL=[env.Install(env['RAD_BINDIR'], prog)])

#surf.cal clockface.hex
LIBFILES = Split('illum.cal rev.cal skybright.cal glaze1.cal glaze2.cal')
env.Append(RAD_RLIBINSTALL=env.Install(env['RAD_RLIBDIR'], LIBFILES))

