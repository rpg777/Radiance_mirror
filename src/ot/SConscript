import os

Import('env') # inherit from parent

# math libs
mlib = env['RAD_MLIB']

# standard targets
env.Object(source='bbox.c')
env.Object(source='o_face.c')
PROGS = (
('oconv',    Split('''oconv.c writeoct.c bbox.o initotypes.c
				   sphere.c o_face.o o_cone.c o_instance.c'''), []),
('getbbox',  Split('getbbox.c readobj2.c bbox.o init2otypes.c'), []),
('obj2mesh', Split('obj2mesh.c cvmesh.c wfconv.c o_face.o writemesh.c'),
	 ['rtdummy']),
)
for p in PROGS:
    prog = env.Program(target=os.path.join(env['RAD_BUILDBIN'], p[0]),
			source=p[1], LIBS=['rtproc','rtscene','rtpath','rtio','rtmath',
			'rtargs','rtcont','rtmem','rterror','rtproc']+p[2]+mlib)
    Default(prog)
    env.Append(RAD_BININSTALL=[env.Install(env['RAD_BINDIR'], prog)])


