import os

Import('env') # inherit from parent

mlib = env['RAD_MLIB']

# compose paths
def radbin(name): return os.path.join(env['RAD_BUILDBIN'], name)
def radlib(name): return os.path.join(env['RAD_BUILDLIB'], name)

# custom compile flags
dielectric = env.Object(source='dielectric.c',
		CCFLAGS=env.get('CCFLAGS',[]) + ['-DDISPERSE'])
# custom C file generation
#env.CFile(target='Version.c', source='VERSION', env=build_radversion)

duphead = env.Object(source="duphead.c")
persist = env.Object(source="persist.c")
ambio = env.Object(source="ambio.c")


# source and object dependencies
RAY = Split('''ambcomp.c ambient.c freeobjmem.c initotypes.c preload.c
		raytrace.c renderopts.c Version.c''') + [ambio]

SURF = Split('''srcsamp.c srcsupp.c srcobstr.c source.c virtuals.c sphere.c
		o_face.c o_cone.c o_instance.c o_mesh.c''')

MAT = Split('''aniso.c normal.c m_clip.c glass.c m_brdf.c m_mirror.c
		m_direct.c m_mist.c fprism.c m_alias.c''') + [dielectric]

MOD = Split('p_func.c t_func.c p_data.c t_data.c text.c mx_func.c mx_data.c')

SUP = Split('func.c noise3.c data.c')

# build libraries
rsrc = RAY + SURF + MAT + MOD + SUP
rlib = env.Library(target=radlib('rttrace'), source=rsrc)

if os.name == 'nt': # XXX until we get around fork() and friends
	rcsrc = Split('raycalls.c')
else: rcsrc = Split('raycalls.c raypcalls.c')
rclib = env.Library(target=radlib('raycalls'), source=rcsrc)
Default(rclib)

fullib=['rttrace','rtscene','rtpic','rtfunc','rtproc','rtio','rtmath','rtargs','rtpath','rtcont','rtmem','rterror']
# build executables
lasrc = ['lookamb.c', ambio]
lookamb = env.Program(target=radbin('lookamb'), source=lasrc,
	LIBS=['rtio', 'rtpic'])
Default(lookamb)
env.Append(RAD_BININSTALL=[env.Install(env['RAD_BINDIR'], lookamb)])

rtmain = env.Object(source=['rtmain.c'],
		CCFLAGS=env.get('CCFLAGS',[]) + ['-DNICE=4'])
rtsrc = ['rtrace.c', duphead, persist, rtmain]
rtrace = env.Program(target=radbin('rtrace'), source=rtsrc,
	LIBS=fullib + ['rtproc'] + mlib) # rtproc for win_nice()
Default(rtrace)
env.Append(RAD_BININSTALL=[env.Install(env['RAD_BINDIR'], rtrace)])

rpsrc = Split('rpmain.c rpict.c srcdraw.c') + [duphead, persist]
rpict = env.Program(target=radbin('rpict'), source=rpsrc,
	LIBS=fullib+['rtnet']+mlib)
Default(rpict)
env.Append(RAD_BININSTALL=[env.Install(env['RAD_BINDIR'], rpict)])

if env.has_key('X11LIB'): # lots of special requirements for rview
	x11incls = [env['X11INCLUDE']] + env['CPPPATH']
	x11 = env.Object(source='x11.c', CPPPATH=x11incls)
	x11twind = env.Object(source='x11twind.c', CPPPATH=x11incls)
	rvmain = env.Object(source='rvmain.c',
		CCFLAGS=env['CCFLAGS'] + ['-DNICE=6'])
	rv3 = env.Object(source='rv3.c', # XXX we should get rid of SPEED
		CCFLAGS=env['CCFLAGS'] + [env.get('RAD_SPEED', '')])

	devsrc  = Split('devtable.c devcomm.c colortab.c editline.c')
	rvobjs = [x11, x11twind, rv3, rvmain, rlib]
	rvsrc = Split('rview.c rv2.c') + devsrc + rvobjs
	rview = env.Program(target=radbin('rview'), source=rvsrc,
		LIBPATH=env['LIBPATH'] + [env['X11LIB']],
		LIBS=fullib + ['X11'] + mlib)
	Default(rview)
	env.Append(RAD_BININSTALL=[env.Install(env['RAD_BINDIR'], rview)])

LIBFILES = Split('rayinit.cal')
env.Append(RAD_RLIBINSTALL=[env.Install(env['RAD_RLIBDIR'], LIBFILES)])

